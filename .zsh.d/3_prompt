#!/bin/zsh

############
# 3 Prompt #
############

# Create var shorcuts for colors
___="%{${reset_color}%}"
____="${reset_color}"
for c in red green yellow white black cyan magenta blue; do
    # escaped with %{ %} for use in prompt
    eval _${c}='%{$fg_no_bold[${(L)c}]%}'
    eval __${c}='%{$fg_bold[${(L)c}]%}'
    # not escaped for use anywhere else
    eval ${c}_='$fg_no_bold[${(L)c}]'
    eval ${c}__='$fg_bold[${(L)c}]'
done

local prmpt_color=""

# Are we root ?
if [ "`id -u`" -eq 0 ];
then
    prmpt_color="${__red} "
else
    local host_sum=$(echo $HOST | sha1sum -)
    host_sum="0x$host_sum[0,1]"
    host_sum=$(( host_sum % 12 + 1))
    if (( host_sum > 6 )); then
        host_sum=$(( host_sum - 6))
        prmpt_color="%B"
    fi
    prmpt_color="${prmpt_color}%F{$host_sum}"
fi

prmpt_color="%(?,${prmpt_color},${__yellow})"

local top="${prmpt_color}"
local bottom="${prmpt_color}"
local time="%*"
local user="${prmpt_color}%n"
local at="${__white}@"
local host="${prmpt_color}%m"
local term="${prmpt_color}/${__blue}$TERM"
local colon="${_magenta}:"
local dollar="${__white}%#"
local ret="${__white}|${_magenta}%?${__white}|"
local stat="%(?,${__blue}^_^ ,${__yellow}-_-')"
local num="${__white}(${prmpt_color}%h${__white})"

local dir="${prmpt_color}%~"
local vcinfo='$(prompt_vc_info)'

bottom="${bottom}${dollar}"

# Sub shell level indicator
if [ $SHLVL -gt 1 ];
then
    for i in {2..$SHLVL}
    do
      bottom="%{%F{$i}%}(${bottom}%{%F{$i}%})"
    done
fi

local prmpt="${top}${dir}${__white}/"
local bprmpt="${bottom}${___} "
local frprmpt="$(prompt_vc_info) "
local prmpt2="${bottom}${__blue}%_ ${__white}>${___} "
local rprmpt="${prmpt_color}${user}${at}${host} ${prmpt_color}${time}${___}"

export PS1="
$prmpt
$bprmpt"

export PS2="${prmpt2}"
export RPS1="${rprmpt}"

padd_prompt() {
  local bprmpt=$bprmpt
  if [ -n "$VIRTUAL_ENV" ]; then
    bprmpt="${__yellow}`basename \"$VIRTUAL_ENV\"` $bprmpt"
  fi
  local prmpt_width=${#${(S%%)prmpt//(\%([KF1]|)\{*\}|\%[Bbkf])}}
  local frprmpt_width=${#${(S%%)frprmpt//(\%([KF1]|)\{*\}|\%[Bbkf])}}
  local padding_size=$(( COLUMNS - prmpt_width - frprmpt_width ))

  if (( padding_size > 0 )); then
    local padding
    eval "padding=\${(l:${padding_size}:: :)_empty_zz}"
    PS1="
${${(%)prmpt}//\//${__white}/${__red}}$padding$frprmpt
$bprmpt"
  fi
}
add-zsh-hook precmd padd_prompt

# xterm title
case $TERM in
    xterm*)
        precmd () {print -Pn "\e]0;%~\a"}
        preexec () { print -Pn "\e]0;$1\a" }
        ;;
esac
